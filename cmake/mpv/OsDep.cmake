if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    set(HAVE_LINUX 1)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
    set(HAVE_DARWIN 1)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "FreeBSD")
    set(HAVE_FREEBSD 1)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "DragonFly")
    set(HAVE_DRAGONFLY 1)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "OpenBSD")
    set(HAVE_OPENBSD 1)
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "NetBSD")
    set(HAVE_NETBSD 1)
endif()

set(TIMER timer-linux.c)
set(GETCH getch2.c)

if(HAVE_DARWIN)
    set(TIMER timer-darwin.c)
elseif(WIN32)
    set(GETCH getch2-win.c)
    set(TIMER timer-win2.c)
    # winmm is always needed for osdep/timer-win2.c
    list(APPEND mpv_LIB winmm)
endif()

set(NEED_SHMEM 1)
if(WIN32 AND NOT CYGWIN)
    unset(NEED_SHMEM)
    add_definitions(-D__USE_MINGW_ANSI_STDIO=1)
    # Hack for missing BYTE_ORDER declarations in <sys/types.h>.
    # (For some reason, they are in <sys/param.h>, but we don't bother switching
    # the includes based on whether we're compiling for MinGW.)
    add_definitions(-DBYTE_ORDER=1234 -DLITTLE_ENDIAN=1234 -DBIG_ENDIAN=4321)
endif()
add_src(NEED_SHMEM osdep/shmem.c)

if(WIN32)
    set(HAVE_DOS_PATHS 1)
    set(CONFIG_PRIORITY 1)
    add_src(CONFIG_PRIORITY osdep/priority.c)
    set(DEFAULT_CDROM_DEVICE "D:")
elseif(HAVE_DARWIN)
    set(DEFAULT_CDROM_DEVICE "/dev/disk1")
elseif(HAVE_DRAGONFLY)
    set(DEFAULT_CDROM_DEVICE "/dev/cd0")
elseif(HAVE_FREEBSD)
    set(DEFAULT_CDROM_DEVICE "/dev/acd0")
elseif(HAVE_OPENBSD)
    set(DEFAULT_CDROM_DEVICE "/dev/rcd0c")
else()
    set(DEFAULT_CDROM_DEVICE "/dev/cdrom")
endif()

if(WIN32 OR HAVE_DRAGONFLY OR HAVE_FREEBSD OR HAVE_OPENBSD)
    set(DEFAULT_DVD_DEVICE ${DEFAULT_CDROM_DEVICE})
elseif(HAVE_DARWIN)
    set(DEFAULT_DVD_DEVICE "/dev/rdiskN")
else()
    set(DEFAULT_DVD_DEVICE "/dev/dvd")
endif()

if(NOT CMAKE_SYSTEM_PROCESSOR)
    message(FATAL_ERROR "Unknown processor type! Please set CMAKE_SYSTEM_PROCESSOR.")
endif()

if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL i386 OR ${CMAKE_SYSTEM_PROCESSOR} STREQUAL x86_64)
    set(HAVE_EBX_AVAILABLE 1)
    set(ARCH_X86 1)
    set(HAVE_MMX 1)
    set(HAVE_MMX2 1)
    set(HAVE_SSE 1)
    set(HAVE_SSE2 1)
    set(HAVE_SSSE3 1)
endif()

if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL i386)
    set(ARCH_X86_32 1)
elseif(${CMAKE_SYSTEM_PROCESSOR} STREQUAL x86_64)
    set(ARCH_X86_64 1)
endif()

if(${CMAKE_SYSTEM_PROCESSOR} STREQUAL ia64 OR ${CMAKE_SYSTEM_PROCESSOR} STREQUAL x86_64)
    set(HAVE_FAST_64BIT 1)
endif()

if(APPLE)
    add_option(CONFIG_MACOSX_FINDER "Mac OS X Finder Support" ON)
    if(CONFIG_MACOSX_FINDER)
        list(APPEND mpv_LIB "-framework Cocoa")
    endif()
    add_src(CONFIG_MACOSX_FINDER osdep/macosx_finder_args.m)

    add_dependent_option(CONFIG_MACOSX_BUNDLE
        "Mac OS X Bundle File Locations" ON
        "CONFIG_MACOSX_FINDER" OFF
    )
    if(CONFIG_MACOSX_BUNDLE)
        list(APPEND mpv_LIB "-headerpad_max_install_names")
    endif()
    add_src(CONFIG_MACOSX_BUNDLE osdep/macosx_bundle.m)
endif()
